# Prime Factory
#
# For each thing in the INBOX,
# send its PRIME FACTORS to the
# OUTBOX in order from
# smallest to largest.

floor size 25

let zero at 24
let one at 23
let two at 22

let original labeled origi
let factor
let divisor
let quotient labeled quot
let remainder labeled rem

## init
one = zero
one += 1
two = one
two += 1

while {
  ## loop
  original = inbox
  factor = original
  divisor = two

  ## factor
  while (factor > one) {
    ## div x
    quotient, remainder = factor ~/ divisor

    ## is div?
    if (remainder == 0) {
      ## is div!
      outbox = divisor
      factor = quotient
    } else {
      ## not div
      if (divisor == two) {
        ## bump 1
        divisor += 1
      } else {
        ## bump 2
        divisor += 1
        divisor += 1
      }
    }
  }
}
